#!/usr/bin/perl

##
## Title:         HS-DeckTrack
## Description:   A Terminal-based Deck Tracker for Hearthstone
## Author:        Dave Russell (bru7us)
## 
## Version:       0.3
## Modified:      21 Jun 2014
##
## License:       MIT (https://github.com/bru7us/HS-DeckTrack/blob/master/LICENSE)
##

use strict;
use warnings;
use Cwd 'abs_path';
use File::Basename;
use Data::Dumper;
use XML::Simple;
use XML::XPath;
use Getopt::Std;
use Env qw(HOME);
use LWP::Simple;

my %opts;
my $sleeptime;
my $logfile;
my $deckfile;
my $savefile;
my %deck_hash;
my %game_hash;
my %op_played_hash;
my %op_discard_hash;
my %hero_discard_hash;
my %hero_burned_hash;
my %hero_mull_hash;
my %hero_drawn_hash;
my %hero_played_hash;
my $DEBUG;
my $count = 1;
my $card;
my $cards_src;
my $cards_db;
my %card_cost;

$card_cost{'The Coin'} = 0;
$card_cost{'Secret'} = '-1';

$| = 1; #Disable print buffering

sub usage {
  select(STDERR);
  printf("\nUsage: [options]");
  printf("\n");
  printf("    -h show this help\n");
  printf("    -d <path> Path to a file containing your deck\n");
  printf("    -l <path> Path to the Hearthstone log file. Default: ~/Library/Logs/Unity/Player.log\n");
  printf("    -m Highlight Milled/Discarded cards");
  printf("    -o Print cards revealed by Opponent");
  printf("    -r <num> sleep for this many seconds between refreshes. Default: 2");
  printf("    -s <path> save the deck out to this file");
  printf("    -X print debug messages to STDOUT\n");
  printf("");
  exit(1);
}

sub print_cards {
  my $cards = shift;
  my $mark = shift // '';
  my $cost;

  foreach my $key (sort { ($card_cost{$a} // '-1') <=> ($card_cost{$b} // '-1') or "\L$a" cmp "\L$b" } keys %{ $cards }) {
    if ((scalar keys %card_cost) > 1) {
      $cost = $card_cost{$key} // '-1';
      $cost = '?' if ($cost < 0); # For presentation purposes
      printf(" %-3s| ",$cost) if $cards->{$key} > 0;
    }
    printf("%-24s %d".$mark."\n", $key, $cards->{$key}) if $cards->{$key} > 0;
  }
}

sub refresh_cards_cache {

  my $found_xml; 
  my $dbtimestamp;
  my $cachetimestamp;

  # Check if cache exists
  $cachetimestamp = load_cache_db();

  # Check if cache is up to date
  $dbtimestamp = (stat $cards_src)[9];
  if ($dbtimestamp == $cachetimestamp) {
    return; #current cache matches the mod time on the db file
  }

  # We get here if cache is stale or doesn't exist
  print "Refreshing cache..." if ($DEBUG);

  # Wrap the root xml element and a timestamp around the card data
  open CARDSRC, '<', $cards_src
    or return;

  open CARDCACHE, '>', $cards_db
    or return;

  print CARDCACHE "<xml>\n";
  print CARDCACHE "<dbtimestamp>".(stat $cards_src)[9]."</dbtimestamp>\n";

  $found_xml = 0;

  while ( my $line = <CARDSRC> ) {
    if ( !$found_xml ) {
      if ($line =~ m/enUS.*<CardDefs>/) {
        $found_xml = 1;
      }
    } else {
      if ($line =~ m/<\/CardDefs>/) {
        last;
      } elsif ($line =~ m/(<\/Entity>).*/) {
        print CARDCACHE $1."\n";
      } elsif ($line =~ m/^\s*<.*>\s*$/) {
        #if ($line =~ m/(Entity|Tag|enUS)/) {
          $line =~ s/\r//g; 
          print CARDCACHE $line;
        #}
      }
    }
  }

  print CARDCACHE "</xml>";

  close CARDCACHE;
  close CARDSRC;

  load_cache_db();
}

sub load_cache_db {
  my $load_failed = 0;
  my $xml = XML::Simple->new;
  my $xmldata;

  if ( -e $cards_db ) {
    $xmldata = $xml->XMLin($cards_db, KeyAttr => { Entity => 'CardID', Tag => 'enumID' }) or $load_failed = 1;
  } else {
    $load_failed = 1;
  }

  if (!$load_failed) {
    foreach my $entity ( keys $xmldata->{Entity} ) {
      if (!($entity =~ m/XXX/)) {
        my $tmpname = $xmldata->{Entity}->{$entity}->{Tag}->{185}->{content} // 0;
        my $tmpcost = $xmldata->{Entity}->{$entity}->{Tag}->{48}->{value} // '-1';
        if ($tmpname) {
          if ( (!defined $card_cost{$tmpname}) || ( (defined $card_cost{$tmpname}) && ($tmpcost ne '-1') ) ) {
            $card_cost{$tmpname} = $tmpcost;
          }
        }
      }
    }
  }

  # If something failed, just proceed without mana cost feature
  if ( $load_failed ) {
    %card_cost = ();
    return 0;
  } else {
    return $xmldata->{dbtimestamp}
  }
}

sub load_deck {

  my $deckref = shift;
  my $path = shift;
  my $deckstring;

  if ( $path =~ /^http.*/ ) {
    if ( $path =~ /^http.*\.{0,1}hearthpwn\.com\/decks\/(\d+)(.*)/ ) {
      $deckstring = get('http://www.hearthpwn.com/decks/'.$1.'/export/1') or return;
    } elsif ( $path =~ /^http.*\.{0,1}hearthhead\.com\/deck=(\d+)\/(.*)/ ) {
      my $page = get($path) or die $!;

      $page =~ m/<div class="deckguide-cards"(.*)<div id="deckguide-cardnotes">/s;
      $page = '<html><div class="deckguide-cards"'.$1.'</html>';

      my $html = XML::XPath->new($page);
      my $names = $html->find('//li');
      for my $node ($names->get_nodelist) {
        $deckstring .= $node->string_value."\n";
      }
    } elsif ( $path =~ /^http.*\.{0,1}hearthstats\.net\/decks\/(.*)/ ) {
      my $page = get($path) or die $!;

      $page =~ m/<div class="deckBuilderCardsWrapper">(.*)<img id="deckBuilderFullCardView"/s;
      $page = '<html><div class="deckBuilderCardsWrapper">'.$1.'</html>';

      my $html = XML::XPath->new($page);
      my $cards = $html->find('//div[@class="deckBuilderCardsWrapper"]/div');
      for my $node ($cards->get_nodelist) {
        my $name = $node->find('div[@class="name"]')->string_value;
        my $count = $node->find('div[@class="qty"]')->string_value;
        $deckstring .= $count."x ".$name."\n";
      }
    } else {
      print "Uh oh! Bad or unsupported URL\n".$path."\nProceeding without a deck...";
      $deckstring = '';
    }
  } else {

    $deckstring = '';

    if ($path ne '') {
      open DECKFILE, $path
        or die "Could not open Deck:\"$path\" for reading: $!\n";
      
      while (my $line = <DECKFILE>) {
        $deckstring .= $line;
      }
      
      close DECKFILE;
    }

  }

  $deckstring =~ s/\s*\<.*\>\s*//g; #Remove markup
  $deckstring =~ s/\r//g; # Remove annoying carriage returns (grumble)
  $deckstring =~ s/\n\s*\n/\n/g; # Remove blank lines
  $deckstring =~ s/\xE2\x80\x99/\x27/g; #Replace 'smart' quotes from TextEdit.app
  chomp($deckstring); # Remove trailing newline before split
    
  foreach my $line (split(/\n/,$deckstring)) {
    chomp($line);
    if ( $line =~ m/^\s*(\d)\s*[xX]*\s*(.*)\s*$/ ) { #<== '2x Soulfire', '2 x Soulfire', '2 Soulfire', '2X Soulfire', etc
      $deckref->{$2} += $1;
    } elsif ( $line =~ m/^\s*(.*?)\s*[xX]*\s*(\d)\s*$/ ) { #<== 'Soulfire x2', 'Soulfire x 2', 'Soulfire 2', etc
      $deckref->{$1} += $2;
    } else {
      $deckref->{$line} += 1;
    }
  }

  my $overwrite_prompt;
  my $overwrite_accept = 'y';

  if ($savefile ne '') {
    if (-e $savefile) {
      print "The file '".$savefile."' already exists. Overwrite? [y/N]: ";
      chomp($overwrite_prompt = lc(<>));
      if ($overwrite_prompt ne $overwrite_accept) {
        return;
      }
    }

    open SAVEFILE, '>', $savefile
      or die "Could not open file:\"$savefile\" to write out deck: $!\n";

    foreach my $card (keys %{ $deckref }) {
      if ($deckref->{$card} > 0) {
        print SAVEFILE $deckref->{$card}." ".$card."\n";
      }
    }

    close SAVEFILE;
  }

}

getopts ('d:hl:mor:s:X', \%opts);
&usage if $opts{'h'};
$DEBUG = 1 if $opts{'X'};
$sleeptime = $opts{'r'} // 2;

$logfile = $opts{l} // $ENV{HOME}.'/Library/Logs/Unity/Player.log';
$deckfile = $opts{d} // '';
$savefile = $opts{s} // '';
$cards_src = '/Applications/Hearthstone/Data/OSX/cardxml0.unity3d';
$cards_db = dirname(abs_path($0)).'/extracted_cards.xml';

print "logfile:" . $logfile . "\n" if ($DEBUG);
print "deckfile:" . $deckfile. "\n" if ($DEBUG);
print "savefile:" . $savefile. "\n" if ($DEBUG);

load_deck(\%deck_hash, $deckfile);

%game_hash = %deck_hash;

&refresh_cards_cache;

while (1) {

  print "\033[2J";   #clear the screen
  print "\033[0;0H"; #jump to 0,0

  open LOGFILE, $logfile
    or die "Could not open Log:\"$logfile\" for reading: $!\n";
  
  while (<LOGFILE>) {
    if (/^\[Zone.*\[name=(.*) id=.*zone=(HAND|PLAY).* FRIENDLY DECK /) {
      $hero_drawn_hash{$1} += 1;
    }
    if (/^\[Zone.*\[name=(.*) id=.*zone=DECK.*from FRIENDLY HAND.*FRIENDLY DECK$/) {
      $hero_mull_hash{$1} += 1;
    }
    if (/^\[Zone.*\[name=(.*) id=.*zone=(HAND|PLAY).* FRIENDLY HAND /) {
      $hero_played_hash{$1} += 1;
    }
    if (/^\[Zone.*\[name=(.*) id=.*zone=GRAVEYARD.* FRIENDLY DECK /) {
      $hero_burned_hash{$1} += 1;
    }
    if (/^\[Zone.*\[name=(.*) id=.*zone=GRAVEYARD.* FRIENDLY HAND /) {
      $hero_discard_hash{$1} += 1;
    }
    if (/^\[Zone.*\[name=(.*) id=.*zone=GRAVEYARD.* OPPOSING (DECK|HAND) /) {
      $op_discard_hash{$1} += 1;
    }
    if (/^\[Zone.*\[name=(.*) id=.*zone=GRAVEYARD.* OPPOSING SECRET /) {
      $op_played_hash{Secret} -= 1;
      $op_played_hash{$1} += 1;
    }
    if (/^\[Zone.*\[name=(.*) id=.*zone=(HAND|PLAY).* OPPOSING HAND /) {
      $op_played_hash{$1} += 1;
    }
    if (/^\[Zone.*\[id=.*zone=SECRET.* OPPOSING HAND /) {
      $op_played_hash{Secret} += 1;
    }
    # Requires 'Bob' logging enabled. Blow away old/previous game data if we found the end of a game
    if (/^\[Bob.*legend rank /) {
      %op_played_hash = ();
      %op_discard_hash = ();
      %hero_discard_hash = ();
      %hero_burned_hash = ();
      %hero_mull_hash = ();
      %hero_drawn_hash = ();
      %hero_played_hash = ();
      %game_hash = %deck_hash;
    }
  }
  
  close LOGFILE;

  if ($DEBUG) {
    print Dumper(\%hero_drawn_hash);
    print Dumper(\%hero_mull_hash);
    print Dumper(\%hero_played_hash);
    print Dumper(\%hero_discard_hash);
    print Dumper(\%hero_burned_hash);
    print Dumper(\%op_played_hash);
    print Dumper(\%op_discard_hash);
    print Dumper(\%game_hash);

    print "Loop count: ".$count++."\n";
  }
  
  # Take cards that have been drawn out of the deck
  foreach $card (keys %hero_drawn_hash) {
    $game_hash{$card} -= $hero_drawn_hash{$card};
    if ( ($game_hash{$card} < 0) && (scalar(keys %deck_hash) == 0) ) {
      # You can't draw more cards than you have, but this is for the case of a null deck to catch/track the mulligans we throw back
      $game_hash{$card} += 1;
    }
  }
  
  # Put back any mulligans
  foreach $card (keys %hero_mull_hash) {
    $game_hash{$card} += $hero_mull_hash{$card};
  }
  
  # Throw away the burned/milled cards
  foreach $card (keys %hero_burned_hash) {
    $game_hash{$card} -= $hero_burned_hash{$card};
  }

  # Dump milled/burned cards into the 'Played' hashes if '-m' selected, otherwise combine.
  if ( $opts{m} ) {
    foreach $card (keys %hero_burned_hash) {
      $hero_discard_hash{$card} += $hero_burned_hash{$card};
    }
  } else {
    foreach $card (keys %hero_burned_hash) {
      $hero_played_hash{$card} += $hero_burned_hash{$card};
    }
    foreach $card (keys %hero_discard_hash) {
      $hero_played_hash{$card} += $hero_discard_hash{$card};
    }
    foreach $card (keys %op_discard_hash) {
      $op_played_hash{$card} += $op_discard_hash{$card};
    }
  }

  # Print the current remaining cards in the deck
  printf("Cards in Deck:\n\n");
  print_cards(\%game_hash);
  
  # Print the cards that you've played
  printf("\n\n\nCards Played:\n\n");
  print_cards(\%hero_played_hash);

  if ( $opts{m} ) {
    # Highlight milled/discarded cards separately
    print_cards(\%hero_discard_hash, '*');
  }
  
  # Print the opponent's revealed cards if '-o' selected
  if ( $opts{o} ) {
    printf("\n\n\nOpponent Cards Revealed:\n\n");
    print_cards(\%op_played_hash);
    if ( $opts{m} ) {
      # Highlight milled/discarded cards separately
      print_cards(\%op_discard_hash, '*');
    }
  }

  if ( $opts{m} ) {
    # Legend
    printf("\n\n*Burned or Discarded");
  }

  sleep $sleeptime;
}
